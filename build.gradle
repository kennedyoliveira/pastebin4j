buildscript {
    repositories { jcenter() }
    dependencies {
        classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    }
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'maven'
apply plugin: 'jacoco'
apply plugin: 'project-report'
apply plugin: 'build-dashboard'
apply plugin: 'findbugs'
apply plugin: 'pmd'

group 'com.github.kennedyoliveira'

sourceCompatibility = 1.8
targetCompatibility = 1.8

wrapper {
    gradleVersion = '4.1'
}

repositories {
    mavenCentral()
}

pmd {
    toolVersion = '5.4.1'
}

tasks.withType(Pmd) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

jacoco {
    toolVersion = '0.7.6.201602180812'
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}

tasks.withType(FindBugs) {
    reports {
        html {
            enabled = true
        }
        xml {
            enabled = false
        }
    }
}

dependencies {
    compileOnly 'org.jetbrains:annotations:15.0'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
}

jar {
    manifest {
        attributes(
                "Implementation-Title": "Pastebin4j",
                "Implementation-Version": version,
                "Implementation-Vendor": "Kennedy Oliveira"
        )
    }
}

//
// Tests configuration
//

// Especial properties for running tests
test {
    systemProperty 'pastebin4j.devkey', System.getProperty('pastebin4j.devkey')
    systemProperty 'pastebin4j.username', System.getProperty('pastebin4j.username')
    systemProperty 'pastebin4j.password', System.getProperty('pastebin4j.password')

    testLogging {
        showStandardStreams = true
        exceptionFormat = 'full'
    }
}
//
// Release information
//

modifyPom {
    project {
        name 'Pastebin4j'
        description 'Implementation of the PasteBin public API..'
        url 'https://github.com/kennedyoliveira/pastebin4j'
        inceptionYear '2015'

        scm {
            url 'https://github.com/kennedyoliveira/pastebin4j'
            connection 'scm:https://kennedyoliveira@github.com/kennedyoliveira/pastebin4j.git'
            developerConnection 'scm:git://github.com/kennedyoliveira/pastebin4j.git'
        }

        licenses {
            license {
                name 'MIT License'
                url 'https://opensource.org/licenses/MIT'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'kennedyoliveira'
                name 'Kennedy Oliveira'
                email 'kennedy.oliveira@outlook.com'
                roles {
                    role 'administrator'
                    role 'developer'
                    role 'owner'
                }
            }
        }
    }
}

nexus {
    sign = true
}